#!/usr/bin/env perl

use common::sense;
use Dir::Self;
use constant THIS => __DIR__ . '/lib';
use lib THIS;

use Date::Format;
use Pod::POM;
use Template;
use View::HTML;
use View::POD;
use View::POD::TOC \@View::POD::toc;

my $template		= new Template { ABSOLUTE => 1 };
my $stash			= do 'config.pm';
my @wrappers		= glob THIS . '/wrappers-enabled/*.tt';

sub parse {
   if (/\.pod$/) {
      my $parser = new Pod::POM;
      my $pom = $parser->parse_file ($_);

      undef @View::POD::toc;
      View::POD::TOC->print ($pom);
      return View::POD->print ($pom);
   } elsif (/\.shtml$/) {
      return do { local $/; open my $fh, '<', $_ or die "$_: $!"; <$fh> };
   } else {
      die "Unknown file type: $_"
   }
}

sub process {
   my ($wrapper, $vars, $page, $ext) = @_;

   my $output;
   $template->process ($wrapper, $vars, \$output)
      or die $template->error;

   open my $fh, '>', "home/$page.$ext"
      or die "home/$page.$ext: $!";
   print $fh $output;
}

for (@ARGV) {
   my ($page) = m!^(?:content|external)/(.+)\.\w+$!;

   my $cur = $page;
   $cur =~ s/\/index$//;

   my $title = $cur;
   $title =~ s/\// \/ /g;
   $title =~ s/\b\w/\u$&/g;

   $View::POD::base = "/home/$cur/";
   my $content = parse;

   my $vars = {
      %$stash,
      title   => $title,
      menubar => (View::HTML::menu "/home/$cur", $stash->{menubar}, '/home/'),
      content => $content,
      updated => (Date::Format::time2str "%C", (stat $_)[9]),
   };

   for (@wrappers) {
      my ($ext) = /(\w+)\.tt$/;
      process $_, $vars, $page, $ext;
   }
}
